$Pattern Образец_погрузки : operation
$Relevant_resources
	_Баржа       : Баржи          Keep Keep
	_Погрузчик   : Погрузчики     Keep Keep
	_Контейнер   : Контейнеры     Keep Keep
$Time = Длительность_обслуживания( _Погрузчик.длительность_min, _Погрузчик.длительность_max )
$Body
_Баржа
	Choice from _Баржа.состояние == Погрузка and _Баржа.грузится_контейнеров + _Баржа.количество_контейнеров < 8
	Convert_begin
		грузится_контейнеров++;
	Convert_end
		количество_контейнеров++;
		грузится_контейнеров--;
		if (_Баржа.количество_контейнеров >= 8)
		{
			состояние = Отплывает;
			расходы_на_топливо+=10;
		}

_Погрузчик
	Choice from _Погрузчик.состояние == Свободен
	with_max ( _Погрузчик.тип_контейнера )
	Convert_begin
		состояние = Занят;

	Convert_end
		состояние = Свободен;
		погружено++;

_Контейнер
	Choice from _Контейнер.состояние == Заказан  and _Контейнер.ширина <= _Погрузчик.тип_контейнера
	first
	Convert_begin
		состояние = Погрузка;
		погрузчик = _Погрузчик.тип_контейнера;
				
	Convert_end
		состояние = Погружен;
		погрузчик = 0;

$End

$Pattern Образец_плавания : operation
$Relevant_resources
	_Баржа       : Баржи         Keep Keep
$Time = Длительность_обслуживания( 6, 8 )
$Body
_Баржа
	Choice from _Баржа.состояние == Отплывает
	Convert_begin
		состояние = Плавание;
	Convert_end
		if (_Баржа.количество_контейнеров == 0)
			состояние = Погрузка;
		else
			состояние = Разгрузка;
		

$End

$Pattern Образец_разгрузки : operation
$Relevant_resources
	_Баржа       : Баржи          NoChange Keep
	_Контейнер   : Контейнеры     Keep Erase
	_Разгрузчик  : Погрузчики     Keep Keep
$Time = Длительность_обслуживания( 4, 5 )
$Body
_Баржа
	Choice from _Баржа.состояние == Разгрузка
	Convert_end
		количество_контейнеров--;
		доход+=_Контейнер.стоимость_перевозки;
		всего_контейнеров++;
		if (_Баржа.количество_контейнеров == 0)
		{
			состояние = Отплывает;
			расходы_на_топливо+=10;
		}
		доход_на_контейнер = _Баржа.доход / _Баржа.всего_контейнеров;

_Контейнер
	Choice from _Контейнер.состояние == Погружен
	Convert_begin
		состояние = Разгрузка;

_Разгрузчик
	Choice from _Разгрузчик.состояние == Свободен and _Разгрузчик.тип_контейнера == 0
	Convert_begin
		состояние = Занят;

	Convert_end
		состояние = Свободен;
		погружено++;
		
$End